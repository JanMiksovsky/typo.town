# HTML specimens for each font
specimens = @map/values(fontData, specimen.ori, { extension: "md->html" })

# Images of each font specimen
specimenImages = @map/values(specimens, screenshot, { extension: "html->png" })

# Cached specimen images so we don't need to constantly run Puppeteer
images = @cache/graph(specimenImages, @files/cache)

# Internal graph of fonts grouped by tag
groupedByTag = groupBy(fontData, =tags)

# Graph for the public-facing part of the site.
# These are the only files that get built and deployed.
public = {

  assets

  # Pages for each font
  fonts = @map/values(fontData, font.ori, { extension: "md->html" })
 
  # Images generated and cached above
  images

  # Home page
  index.html = index.ori()

  # Pages for each tag
  tags = @map/values(groupedByTag, tag.ori, { extension: "->html" })

}
